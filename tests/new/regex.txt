LBRACK		 \[
RBRACK		 \]
PLUS		      \+
MINUS		 \-
TIMES	      \*
NEWLINE_CHAR    n
CRETURN_CHAR    r
TABULATE_CHAR   t
QUESTION	      ?
BAR		      \|
SLASH		 \\
CAP		      ^
LPAREN		 \(
RPAREN		 \)
LBRACE          \{
RBRACE          \}
CHAR		      [\w!#-&\(-\[\]-\}]
DOT             \.

%%

%start re

re   => [ exp { BAR exp } ] ;

exp  => msub { msub } ;

msub => sub [ QUESTION | PLUS | TIMES ] ;

sub  => elm
     | LBRACK slc RBRACK
     | LPAREN re RPAREN
     ;

elm  => fch
     | CAP
     | MINUS
     | DOT
     ;

esc  => SLASH QUESTION
     | SLASH LBRACK
     | SLASH RBRACK
     | SLASH PLUS
     | SLASH MINUS
     | SLASH TIMES
     | SLASH BAR
     | SLASH SLASH
     | SLASH CAP
     | SLASH LPAREN
     | SLASH RPAREN
     | SLASH DOT
     | SLASH NEWLINE_CHAR
     | SLASH CRETURN_CHAR
     | SLASH TABULATE_CHAR
     ;

slc => [ CAP ] sli { sli } ;

sli => fch 
     | fch MINUS fch
     | DOT
     ;

fch => CHAR
     | TABULATE_CHAR
     | NEWLINE_CHAR
     | CRETURN_CHAR
     | esc
     ;